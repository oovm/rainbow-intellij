{
    parserClass = "com.github.voml.rainbow_intellij.language.parser.RbParser"
    parserUtilClass = "com.github.voml.rainbow_intellij.language.psi.RbParserUtil"



    psiClassPrefix = "Rain"
    psiImplClassSuffix = "Node"
    psiPackage = "com.github.voml.rainbow_intellij.language.psi"
    psiImplPackage = "com.github.voml.rainbow_intellij.language.psi_node"

    elementTypeHolderClass = "com.github.voml.rainbow_intellij.language.psi.RbToken"
    elementTypeClass = "com.github.voml.rainbow_intellij.language.psi.RbElementType"
    tokenTypeClass = "com.github.voml.rainbow_intellij.language.psi.RbTokenType"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"
    psiImplUtilClass = "com.github.voml.rainbow_intellij.language.ast.RainAstExtension"

    tokens = [
        // comment
        COMMENT_DOCUMENT = 'Comment Document'
        COMMENT = 'Comment'
        COMMENT_BLOCK = 'Comment Block'

        // literal
        URL = 'Url'
        SYMBOL = 'Symbol'
        STRING = 'String'
        COLOR = 'Color'

        INTEGER = 'Integer'
        DECIMAL = 'Decimal'
        SIGN = 'regexp:[+-]'

        PARENTHESIS_L = '('
        PARENTHESIS_R = ')'
        BRACKET_L = '['
        BRACKET_R = ']'
        BRACE_L = '{'
        BRACE_R = '}'
        ANGLE_L = '<'
        ANGLE_R = '>'
        ACCENT = '^'
        EQ = '='
        COLON = ':'
        SEMICOLON = ';'
        COMMA = ','
        DOLLAR = '$'
        DOT = '.'
        STAR = '*'
        AT = '@'
//      SPACE='regexp:\s+'
//      ANY = 'regexp:.'
    ]
}

Rb ::= statement*;

private statement ::=
    schema_statement
  | meta_statement
  | language_statement
  | COMMENT_DOCUMENT
  | ignore;
private ignore ::= SEMICOLON | COMMA;
// ---------------------------------------------------------------------------------------------------------------------
schema_statement ::= "schema" identifier [COLON inherit] <<brace_block field_statement ignore>> {
    mixin = "com.github.voml.rainbow_intellij.language.mixin.MixinSchema"
    methods = [getKeyword]
};
// ---------------------------------------------------------------------------------------------------------------------
meta_statement ::= identifier <<brace_block field_statement ignore>> {
    mixin = "com.github.voml.rainbow_intellij.language.mixin.MixinMeta"
};
// ---------------------------------------------------------------------------------------------------------------------
language_statement ::= "language" identifier [COLON inherit] <<brace_block field_statement ignore>> {
    mixin = "com.github.voml.rainbow_intellij.language.mixin.MixinLanguage"
    methods = [getKeyword getFields]
};
inherit ::= identifier;
object_inherit ::= namespace? object;
// ---------------------------------------------------------------------------------------------------------------------
field_statement ::= key eq (object_inherit|array) {
    mixin = "com.github.voml.rainbow_intellij.language.mixin.MixinField"
    // recoverWhile = brace_recover
};
private brace_recover ::= !(BRACE_R);
// ---------------------------------------------------------------------------------------------------------------------
array ::= (!(SYMBOL eq) value)+ ;
object ::= <<brace_block field_statement ignore>>;
// ---------------------------------------------------------------------------------------------------------------------
key ::= string_literal | identifier;
private eq ::= EQ | COLON;
value ::= data | namespace;
private data ::= literal | string_literal | color_literal | number;
// ---------------------------------------------------------------------------------------------------------------------
number ::= [SIGN] (INTEGER | DECIMAL);
namespace  ::= identifier (DOT identifier)* {
    mixin = "com.github.voml.rainbow_intellij.language.mixin.MixinNamespace"
};
identifier ::= SYMBOL;
color_literal ::= COLOR;
string_literal ::= STRING;
literal ::= "true" | "false" | "null";
// ---------------------------------------------------------------------------------------------------------------------
// parenthesis ( )
private meta parenthesis ::= PARENTHESIS_L <<param>> PARENTHESIS_R;
// bracket [ ]
meta bracket_block ::= BRACKET_L [<<item>> (<<sp>> <<item>>)* [<<sp>>]] BRACKET_R;
// brace { }
meta brace_block ::=BRACE_L (<<item>>|<<sp>>)* BRACE_R;

