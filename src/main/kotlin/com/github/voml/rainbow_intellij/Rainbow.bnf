{
    parserClass = "com.github.voml.rainbow_intellij.language.parser.RbParser"
    parserUtilClass = "com.github.voml.rainbow_intellij.language.psi.RbParserUtil"



    psiClassPrefix = "Rain"
    psiImplClassSuffix = "Node"
    psiPackage = "com.github.voml.rainbow_intellij.language.psi"
    psiImplPackage = "com.github.voml.rainbow_intellij.language.psi_node"

    elementTypeHolderClass = "com.github.voml.rainbow_intellij.language.psi.RbToken"
    elementTypeClass = "com.github.voml.rainbow_intellij.language.psi.RbElementType"
    tokenTypeClass = "com.github.voml.rainbow_intellij.language.psi.RbTokenType"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"
    psiImplUtilClass = "com.github.voml.rainbow_intellij.language.ast.RainAstExtension"

    tokens = [
        // comment
        COMMENT_DOCUMENT = 'Comment Document'
        COMMENT = 'Comment'
        COMMENT_BLOCK = 'Comment Block'

        // literal
        URL = 'Url'
        SYMBOL = 'Symbol'
        STRING_RAW = 'String'

        BYTE = 'Byte'
        INTEGER = 'Integer'
        DECIMAL = 'Decimal'
        SIGN = 'regexp:[+-]'

        PARENTHESIS_L = '('
        PARENTHESIS_R = ')'
        BRACKET_L = '['
        BRACKET_R = ']'
        BRACE_L = '{'
        BRACE_R = '}'
        ANGLE_L = '<'
        ANGLE_R = '>'
        ACCENT = '^'
        EQ = '='
        COLON = ':'
        SEMICOLON = ';'
        COMMA = ','
        DOLLAR = '$'
        DOT = '.'
        STAR = '*'
        AT = '@'
//      SPACE='regexp:\s+'
//      ANY = 'regexp:.'
    ]
}

Rb ::= statement*;

private statement ::=
    schema_statement
  | meta_statement
  | global_statement
  | COMMENT_DOCUMENT
  | ignore;
private ignore ::= SEMICOLON | COMMA;
// ---------------------------------------------------------------------------------------------------------------------
// schema <ID>: <TYPE> { }
schema_statement ::= "schema" identifier <<brace_block properties_inner ignore>> {
    mixin = "com.github.voml.rainbow_intellij.language.mixin.MixinSchema"
    methods = [getSchema]
};
// ---------------------------------------------------------------------------------------------------------------------
meta_statement ::= "meta" <<brace_block properties_inner ignore>> {
    mixin = "com.github.voml.rainbow_intellij.language.mixin.MixinMeta"
    methods = [getMeta]
};
// ---------------------------------------------------------------------------------------------------------------------
global_statement ::= "global" <<brace_block properties_inner ignore>> {
    mixin = "com.github.voml.rainbow_intellij.language.mixin.MixinGlobal"
    methods = [getGlobal]
};
// ---------------------------------------------------------------------------------------------------------------------
// properties <ID>: <TYPE> { }
field_statement ::= key eq value+ {
    mixin = "com.github.voml.rainbow_intellij.language.mixin.MixinField"
};
key ::= STRING_RAW | SYMBOL;
value ::= null | boolean | number | string | array | object | url_maybe_valid | identifier;
// used in formatter
private properties_inner ::=
    field_statement
  | COMMENT_DOCUMENT
  | kv_pair // consider as anno
  ;
// ---------------------------------------------------------------------------------------------------------------------
//symbol_path ::= key (DOT key)*;
//private key ::= string_inline | key_symbol | INTEGER;
private eq ::= EQ | COLON;
// ---------------------------------------------------------------------------------------------------------------------
null ::= "null";
boolean ::= "true" | "false";
// used in annotation
url_maybe_valid ::= URL;
// ---------------------------------------------------------------------------------------------------------------------
number ::= [SIGN] (INTEGER | DECIMAL) | [SIGN] BYTE;
// ---------------------------------------------------------------------------------------------------------------------
string ::= (string_inline|string_multi);
string_inline ::= STRING_RAW;
string_multi ::= STRING_RAW;
// ---------------------------------------------------------------------------------------------------------------------
array ::= <<bracket_block value ignore>>;
object ::= <<brace_block kv_pair ignore>>;
kv_pair ::= (string_inline|SYMBOL) eq value;
// ---------------------------------------------------------------------------------------------------------------------
identifier ::= SYMBOL;
// ---------------------------------------------------------------------------------------------------------------------
// parenthesis ( )
private meta parenthesis ::= PARENTHESIS_L <<param>> PARENTHESIS_R;
// bracket [ ]
meta bracket_block ::= BRACKET_L [<<item>> (<<sp>> <<item>>)* [<<sp>>]] BRACKET_R;
// brace { }
meta brace_block ::=BRACE_L (<<item>>|<<sp>>)* BRACE_R;

